buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:1.1.3"
        classpath "com.jakewharton.sdkmanager:gradle-plugin:0.12.0"
    }
}

apply plugin: "android-sdk-manager"
apply plugin: "com.android.application"

android {
    compileSdkVersion 22
    buildToolsVersion "22"

    defaultConfig {
        def versionMajor = 1
        def versionMinor = 2
        def versionPatch = 0

        versionName buildVersionName(versionMajor, versionMinor, versionPatch)
        versionCode buildVersionCode(versionMajor, versionMinor, versionPatch)

        minSdkVersion 14
        targetSdkVersion 22

        resConfigs "nodpi", "hdpi", "xhdpi", "xxhdpi"
        resConfigs "en", "ru", "de"
    }

    signingConfigs {
        release {
            def signingFile = file("signing.properties")
            def signingProperties = new Properties()

            if (signingFile.exists()) {
                signingProperties.load(signingFile.newInputStream())

                storeFile file(signingProperties["keystore.file"])
                storePassword signingProperties["keystore.password"]
                keyAlias signingProperties["key.alias"]
                keyPassword signingProperties["key.password"]
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude "META-INF/services/javax.annotation.processing.Processor"
    }
}

def buildVersionName(versionMajor, versionMinor, versionPatch) {
    return "${versionMajor}.${versionMinor}.${versionPatch}"
}

def buildVersionCode(versionMajor, versionMinor, versionPatch) {
    return versionMajor * 10 ** 2 + versionMinor * 10 ** 1 + versionPatch * 10 ** 0
}

dependencies {
    repositories {
        mavenCentral()
    }

    compile "com.android.support:appcompat-v7:22.0.0"
    compile "com.android.support:cardview-v7:22.0.0"
    compile "com.android.support:support-v4:22.0.0"
    compile "com.android.support:support-annotations:22.0.0"
    compile "com.f2prateek.bundler:bundler:1.1.0"
    compile "com.f2prateek.dart:dart:1.1.0"
    compile "com.jakewharton:butterknife:6.1.0"
    compile "com.google.android.gms:play-services-drive:7.0.0"
    compile "com.guerwan:transitionbackport:1.1"
    compile 'com.melnykov:floatingactionbutton:1.2.0'
    compile "com.mcxiaoke.viewpagerindicator:library:2.4.1"
    compile "com.rengwuxian.materialedittext:library:1.8.3"
    compile "com.squareup:otto:1.3.6"
    compile "com.squareup:seismic:1.0.1"
    compile "com.venmo.cursor:library:0.4"
    compile "commons-io:commons-io:2.4"
}

task generateWrapper(type: Wrapper) {
    description "Generates Gradle wrapper."

    gradleVersion = "2.3"
}
