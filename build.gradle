buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:0.12.+"
        classpath "com.jakewharton.sdkmanager:gradle-plugin:0.12.+"
    }
}

apply plugin: "android-sdk-manager"
apply plugin: "android"

android {
    compileSdkVersion 19
    buildToolsVersion "20"

    defaultConfig {
        def versionMajor = 1
        def versionMinor = 1
        def versionPatch = 1

        versionName buildVersionName(versionMajor, versionMinor, versionPatch)
        versionCode buildVersionCode(versionMajor, versionMinor, versionPatch)

        minSdkVersion 14
        targetSdkVersion 19

        resConfigs "nodpi", "hdpi", "xhdpi", "xxhdpi"
        resConfigs "en", "ru", "de"
    }

    signingConfigs {
        release {
            def signingFile = file("signing.properties")
            def signingProperties = new Properties()

            if (signingFile.exists()) {
                signingProperties.load(signingFile.newInputStream())

                storeFile file(signingProperties["keystore.file"])
                storePassword signingProperties["keystore.password"]
                keyAlias signingProperties["key.alias"]
                keyPassword signingProperties["key.password"]
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

def buildVersionName(versionMajor, versionMinor, versionPatch) {
    return "${versionMajor}.${versionMinor}.${versionPatch}"
}

def buildVersionCode(versionMajor, versionMinor, versionPatch) {
    return versionMajor * 10 ** 2 + versionMinor * 10 ** 1 + versionPatch * 10 ** 0
}

dependencies {
    repositories {
        maven {
            url "http://dl.bintray.com/populov/maven"
        }

        mavenCentral()
    }

    compile "com.android.support:support-v4:20.0.+"
    compile "com.google.android.gms:play-services:5.2.+"
    compile "com.guerwan:transitionbackport:1.1"
    compile "com.squareup:otto:1.3.+"
    compile "com.squareup:seismic:1.0.+"
    compile "com.viewpagerindicator:library:2.4.1"
    compile "commons-io:commons-io:2.4"
}

task generateWrapper(type: Wrapper) {
    description "Generates Gradle wrapper."

    gradleVersion = "1.12"
}
